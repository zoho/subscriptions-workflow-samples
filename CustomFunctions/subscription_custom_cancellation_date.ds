/*
	
  This is a sample function written for automating your workflow in Zoho Subscriptions, a recurring billing and subscription
  management software.

  Automation Module - Custom Schedulers

  Frequency         - Daily

  Place your {{connection name}} in "YOUR_CONNECTION_NAME" variables.

  * This custom function is used to configure a custom scheduler to cancel a subscription at a fixed (custom) date with the
    help of custom fields.
	
*/

organizationID = organization.get("organization_id");

// Enter the corresponding customViewID and customFieldID here.
customViewID  = "";
customFieldID = "";
domain = "https://www.zohoapis.com/billing/v1";
currentDate = zoho.currentdate.toString("yyyy-MM-dd");

url = domain+"/subscriptions?customview_id=" + customViewID + "&organization_id=" + organizationID;

request  = "Retrieving Subscriptions";

response = invokeUrl [
    url : url
    type : GET
    connection : "YOUR_CONNECTION_NAME"
];

if(response.get("code") != 0)
{
	errorMessage = response.get("message");
	sendmail
	[
		from :zoho.adminuserid
		to :zoho.adminuserid
		subject :"Error occured in Subscription Cancellation Scheduler while " + request
		message :"<b>Affected url :</b><br>" + url + "<br><b>Error Message</b><br>" + errorMessage
	]
	return;
}

subscriptionList = response.get("subscriptions");

params = Map();
params.put("cancel_at_end",false);

// Enter the appropriate reason for cancelling the subscription b/w the quotes.
params.put("reason", "");

for each  subscription in subscriptionList
{
	if(subscription.get("status").equals("live"))
	{
		customFieldsList = subscription.get("custom_fields");

		for each  customField in customFieldsList
		{
			if(customField.get("customfield_id").equals(customFieldID))
			{
				if(customField.get("value").equals(currentDate))
				{
					subscriptionID = subscription.get("subscription_id");

					cancelUrl = domain+"/subscriptions/" + subscriptionID + "/cancel?organization_id=" + organizationID;

					response = invokeUrl [
    					url : cancelUrl
    					type : POST
    					parameters : params.toString()
    					connection : "YOUR_CONNECTION_NAME"
					];
					if(response.get("code") != 0)
					{
						errorMessage = response.get("message");
						sendmail
						[
							from :zoho.adminuserid
							to :zoho.adminuserid
							subject :"Error occured in Subscription Cancellation Scheduler"
							message :"Error in cancelling Subscription (Subscription ID -" + subscriptionID + ")  <br>Error Message:" + errorMessage
						]
					}
				}
				break;
			}
	 	}
	}
}
